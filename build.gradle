import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.3.0'
    }
}

// Charts main build file
plugins {
    id 'com.gradle.build-scan' version '1.11'
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '2.6.1'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

javafx {
    modules = [ 'javafx.controls', 'javafx.swing' ]
}

apply plugin: 'biz.aQute.bnd.builder'

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

description = 'Charts is a JavaFX library containing different kind of charts'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

dependencies {}

jar {
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date': project.buildDate,
                'Build-Time': project.buildTime,
                'Build-Revision': versioning.info.commit,
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Bundle-Name': project.name,
                'Bundle-License': 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://www.eclipse.org/legal/eplfaq.php',
                'Bundle-Description': description,
                'Bundle-SymbolicName': 'eu.hansolo.fx.charts',
                'Export-Package': 'eu.hansolo.fx.charts,eu.hansolo.fx.charts.areaheatmap,eu.hansolo.fx.charts.color,eu.hansolo.fx.charts.data,eu.hansolo.fx.charts.event,eu.hansolo.fx.charts.font,eu.hansolo.fx.charts.heatmap,eu.hansolo.fx.charts.series,eu.hansolo.fx.charts.tools,eu.hansolo.fx.charts.tree,eu.hansolo.fx.charts.world,eu.hansolo.fx.geometry,eu.hansolo.fx.geometry.tools,eu.hansolo.fx.geometry.transform'
        )
    }
}

repositories {
    jcenter()
    mavenCentral()
}


// create one jar for the javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

def pomConfig = {
    name 'charts'
    // Description won't be used when creating the pom file automatically.
    // So I've added it manually in the pom.withXml{} section in the publishing task
    //description 'Charts is a JavaFX 8 library containing different charts'
    url 'https://github.com/HanSolo/charts/wiki'
    inceptionYear '2017'
    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'scm:git@github.com:HanSolo/charts.git'
        connection 'scm:git@github.com:HanSolo/charts.git'
        developerConnection 'scm:git@github.com:HanSolo/charts.git'
    }
    developers {
        developer {
            id 'HanSolo'
            name 'Gerrit Grunwald'
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + pomConfig
                asNode().appendNode('description', description)
            }
        }
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key  = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo                  = 'charts'
        userOrg               = 'hansolo'
        name                  = project.name
        desc                  = description
        licenses              = ['Apache-2.0']
        labels                = ['javafx']
        websiteUrl            = 'https://github.com/HanSolo/charts/wiki'
        issueTrackerUrl       = 'https://github.com/HanSolo/charts/issues'
        vcsUrl                = 'git@github.com:HanSolo/charts.git'
        publicDownloadNumbers = true
    }
}